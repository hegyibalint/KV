package hu.bme.mit.kv.queries

import "http://hu.bme.mit.kv.railroadmodel"

pattern pointIsFurtherAway(t : Train, p1 : Point, p2 : Point){
	p1 != p2;
	Train.x(t,tx);
	Train.y(t,ty);
	Point.x(p1,p1x);
	Point.y(p1,p1y);
	Point.x(p2,p2x);
	Point.y(p2,p2y);
	
	check( ((tx-p1x)*(tx-p1x) + (ty-p1y)*(ty-p1y)) < ((tx-p2x)*(tx-p2x) + (ty-p2y)*(ty-p2y)));
}

pattern closestPointToTrain(t : Train, p : Point){
	neg find pointIsFurtherAway(t, _, p);
}

pattern closestSectionToTrain(t : Train, s : Section){
	Point(p);
	find closestPointToTrain(t, p);
	Section.points(s, p);
}

pattern sectionBeforeClockwiseTrain(t: Train, s: Section) {
	Train.goingClockwise(t, true);
	Train.currentlyOn(t, currentSection);
	Section.clockwise(currentSection, s);
}

pattern sectionBeforeCounterClockwiseTrain(t : Train, s : Section) { //TODO tina turner
	Train.goingClockwise(t, false);
	Train.currentlyOn(t, currentSection);
	Section.counterClockwise(currentSection, s);
}

pattern sectionBeforeTrain(t : Train, s : Section){
	find sectionBeforeCounterClockwiseTrain(t, s);
} or {
	find sectionBeforeClockwiseTrain(t,s);
}

pattern sameSection(t1 : Train, t2 : Train){
	t1 != t2;
	Train.currentlyOn(t1, section);
	Train.currentlyOn(t2, section);
}

pattern zeroBetween(t1 : Train, t2 : Train){
	t1 != t2; //Feel free to remove this when the turner has been implemented
	Train.currentlyOn(t1, section);
	find sectionBeforeTrain(t2, section);
}

pattern oneBetween(t1 : Train, t2 : Train){
	t1 != t2;//Feel free to remove this when the turner has been implemented
	find sectionBeforeTrain(t1, section);
	find sectionBeforeTrain(t2, section);
}

pattern safe(t1: Train, t2: Train){
	t1 != t2;
	0 == count find sameSection(t1, t2);
	0 == count find zeroBetween(t1,t2);
	0 == count find oneBetween(t1,t2);

}
